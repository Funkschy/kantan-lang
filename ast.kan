#include "expr_types.h"
#include "stmt_types.h"

import "std";
import "span";
import "ident";
import "func";
import "record";
import "ptrvec";
import "tyid";

import "io"; // TODO: only debug

extern def binary2str(op: i32): string;
extern def unary2str(op: i32): string;

/////////// expr stmt ///////////

type ExprStmt struct {
    s: Stmt,
    expr: *Expr
}

def make_expr_stmt(e: *Expr): *Stmt {
    return (new ExprStmt { s: Stmt { tag: STMT_EXPR }, expr: e }) as *Stmt;
}

def is_expr_stmt(s: *Stmt): bool {
    return s.tag == STMT_EXPR;
}

def as_expr_stmt(s: *Stmt): *ExprStmt {
    return s as *ExprStmt;
}

/////////// New stmt ///////////

type NewStmt struct {
    s: Stmt,
    expr: *Expr
}

def make_new_stmt(expr: *Expr): *Stmt {
    return (new NewStmt {
        s: Stmt { tag: STMT_NEW },
        expr: expr
    }) as *Stmt;
}

def is_new_stmt(s: *Stmt): bool {
    return s.tag == STMT_NEW;
}

def as_new_stmt(s: *Stmt): *NewStmt {
    return s as *NewStmt;
}

/////////// Delete stmt ///////////

type DeleteStmt struct {
    s: Stmt,
    expr: *Expr
}

def make_delete_stmt(expr: *Expr): *Stmt {
    return (new DeleteStmt {
        s: Stmt { tag: STMT_DELETE },
        expr: expr
    }) as *Stmt;
}

def is_delete_stmt(s: *Stmt): bool {
    return s.tag == STMT_DELETE;
}

def as_delete_stmt(s: *Stmt): *DeleteStmt {
    return s as *DeleteStmt;
}

/////////// Return stmt ///////////

type ReturnStmt struct {
    s: Stmt,
    expr: *Expr
}

def make_return_stmt(expr: *Expr): *Stmt {
    return (new ReturnStmt {
        s: Stmt { tag: STMT_RETURN },
        expr: expr
    }) as *Stmt;
}

def is_return_stmt(s: *Stmt): bool {
    return s.tag == STMT_RETURN;
}

def as_return_stmt(s: *Stmt): *ReturnStmt {
    return s as *ReturnStmt;
}

/////////// While stmt ///////////

type WhileStmt struct {
    s: Stmt,
    condition: *Expr,
    block: *BlockStmt
}

def make_while_stmt(condition: *Expr, block: *BlockStmt): *Stmt {
    return (new WhileStmt {
        s: Stmt { tag: STMT_WHILE },
        condition: condition,
        block: block,
    }) as *Stmt;
}

def is_while_stmt(s: *Stmt): bool {
    return s.tag == STMT_WHILE;
}

def as_while_stmt(s: *Stmt): *WhileStmt {
    return s as *WhileStmt;
}

/////////// If stmt ///////////

type IfStmt struct {
    s: Stmt,
    condition: *Expr,
    block: *BlockStmt,
    else_stmt: *Stmt // either IfStmt or BlockStmt
}

def make_if_stmt(condition: *Expr, block: *BlockStmt, else_stmt: *Stmt): *Stmt {
    return (new IfStmt {
        s: Stmt { tag: STMT_IF },
        condition: condition,
        block: block,
        else_stmt: else_stmt
    }) as *Stmt;
}

def is_if_stmt(s: *Stmt): bool {
    return s.tag == STMT_IF;
}

def as_if_stmt(s: *Stmt): *IfStmt {
    return s as *IfStmt;
}

def else_type(s: *IfStmt): i32 {
    if s.else_stmt == null {
        return type_no_else();
    }

    if is_block_stmt(s.else_stmt) {
        return type_else_block();
    }

    if is_if_stmt(s.else_stmt) {
        return type_else_if();
    }

    return type_no_else();
}

def type_no_else(): i32 {
    return 0;
}

def type_else_block(): i32 {
    return 1;
}

def type_else_if(): i32 {
    return 2;
}

/////////// Block stmt ///////////

type BlockStmt struct {
    s: Stmt,
    stmts: ptrvec.Vec
}

def make_block_stmt(statements: ptrvec.Vec): *Stmt {
    return (new BlockStmt{
        s: Stmt { tag: STMT_BLOCK },
        stmts: statements
    }) as *Stmt;
}

def is_block_stmt(s: *Stmt): bool {
    return s.tag == STMT_BLOCK;
}

def as_block_stmt(s: *Stmt): *BlockStmt {
    return s as *BlockStmt;
}

/////////// Func Decl stmt ///////////

type FuncDeclStmt struct {
    s: Stmt,
    decl: func.FuncDef,
    block: *BlockStmt
}

def make_func_decl_stmt(ident: lexer.Token, params: func.ParamList, ret_ty: tyid.Type, block: *BlockStmt): *Stmt {
    return (new FuncDeclStmt {
        s: Stmt { tag: STMT_FUNC_DECL },
        decl: func.create(ident, params, ret_ty),
        block: block
    }) as *Stmt;
}

def is_func_decl_stmt(s: *Stmt): bool {
    return s.tag == STMT_FUNC_DECL;
}

def as_func_decl_stmt(s: *Stmt): *FuncDeclStmt {
    return s as *FuncDeclStmt;
}

/////////// Struct Decl stmt ///////////

type StructDeclStmt struct {
    s: Stmt,
    decl: record.StructDecl
}

def make_struct_decl_stmt(si: *span.SpanInterner, name: ident.Ident, fields: vec.Vec): *Stmt {
    return (new StructDeclStmt {
        s: Stmt { tag: STMT_STRUCT_DECL },
        decl: record.create_struct(si, name, fields)
    }) as *Stmt;
}

def is_struct_decl_stmt(s: *Stmt): bool {
    return s.tag == STMT_STRUCT_DECL;
}

def as_struct_decl_stmt(s: *Stmt): *StructDeclStmt {
    return s as *StructDeclStmt;
}

/////////// import stmt ///////////

type ImportStmt struct {
    s: Stmt,
    path_span: span.Span,
    mod_path: string
}

def make_import_stmt(span: span.Span, mod_path: string): *Stmt {
    return (new ImportStmt {
        s: Stmt { tag: STMT_IMPORT },
        path_span: span,
        mod_path: mod_path
    }) as *Stmt;
}

def is_import_stmt(s: *Stmt): bool {
    return s.tag == STMT_IMPORT;
}

def as_import_stmt(s: *Stmt): *ImportStmt {
    return s as *ImportStmt;
}

/////////// let stmt ///////////

type LetStmt struct {
    s: Stmt,
    value: *Expr,
    ident: ident.Ident
}

def make_let_stmt(ident: lexer.Token, value: *Expr): *Stmt {
    return (new LetStmt {
        s: Stmt { tag: STMT_LET },
        value: value,
        ident: ident.create(ident)
    }) as *Stmt;
}

def is_let_stmt(s: *Stmt): bool {
    return s.tag == STMT_LET;
}

def as_let_stmt(s: *Stmt): *LetStmt {
    return s as *LetStmt;
}

/////////// base stmt ///////////

type Stmt struct {
    tag: i32
}

def stmt_free(s: *Stmt): void {
    if s == null {
        return;
    }

    if is_expr_stmt(s) {
        expr_free(as_expr_stmt(s).expr);
    } else if is_let_stmt(s) {
        expr_free(as_let_stmt(s).value);
    } else if is_new_stmt(s) {
        expr_free(as_new_stmt(s).expr);
    } else if is_delete_stmt(s) {
        expr_free(as_delete_stmt(s).expr);
    } else if is_return_stmt(s) {
        expr_free(as_return_stmt(s).expr);
    } else if is_func_decl_stmt(s) {
        let f_decl = as_func_decl_stmt(s);
        let decl = f_decl.decl;
        func.free_f(&decl);
        stmt_free(f_decl.block as *Stmt);
    } else if is_block_stmt(s) {
        let stmts = as_block_stmt(s).stmts;

        let i = 0;
        while i < stmts.len {
            let s = ptrvec.get(&stmts, i);
            if s != null {
                stmt_free(s as *Stmt);
            }
            i = i + 1;
        }

        ptrvec.free_v(&stmts);
    } else if is_if_stmt(s) {
        let if_stmt = as_if_stmt(s);
        expr_free(if_stmt.condition);
        stmt_free(if_stmt.block as *Stmt);
        stmt_free(if_stmt.else_stmt);
    } else if is_while_stmt(s) {
        let while_stmt = as_while_stmt(s);
        expr_free(while_stmt.condition);
        stmt_free(while_stmt.block as *Stmt);
    } else if is_struct_decl_stmt(s) {
        let s_stmt = as_struct_decl_stmt(s);
        record.free_struct(&s_stmt.decl);
    }

    delete s;
}

/////////// access expr ///////////

type AccessExpr struct {
    e: Expr,
    left: *Expr,
    right: *Expr
}

def make_access(span: span.Span, left: *Expr, right: *Expr): *Expr {
    return (new AccessExpr {
        e: Expr { tag: EXPR_ACCESS, span: span },
        left: left,
        right: right
    }) as *Expr;
}

def is_access(e: *Expr): bool {
    return e.tag == EXPR_ACCESS;
}

def as_access(e: *Expr): *AccessExpr {
    return e as *AccessExpr;
}

/////////// call expr ///////////

type CallExpr struct {
    e: Expr,
    ident: *Expr, // either IdentExpr or AccessExpr
    args: ArgList
}

def make_call(span: span.Span, ident: *Expr, args: ArgList): *Expr {
    if !is_ident(ident) && !is_access(ident) {
        expr_free(ident);
        return null;
    }

    return (new CallExpr {
        e: Expr { tag: EXPR_CALL, span: span },
        ident: ident,
        args: args
    }) as *Expr;
}

def is_call(e: *Expr): bool {
    return e.tag == EXPR_CALL;
}

def as_call(e: *Expr): *CallExpr {
    return e as *CallExpr;
}

/////////// Init expr ///////////

type InitExpr struct {
    e: Expr,
    ident: *Expr, // either IdentExpr or AccessExpr
    inits: InitList
}

def make_init(span: span.Span, ident: *Expr, inits: InitList): *Expr {
    if !is_ident(ident) && !is_access(ident) {
        expr_free(ident);
        return null;
    }

    return (new InitExpr {
        e: Expr { tag: EXPR_INIT, span: span },
        ident: ident,
        inits: inits
    }) as *Expr;
}

def is_init(e: *Expr): bool {
    return e.tag == EXPR_INIT;
}

def as_init(e: *Expr): *InitExpr {
    return e as *InitExpr;
}

/////////// assign expr ///////////

type AssignExpr struct {
    e: Expr,
    left: *Expr,
    right: *Expr
}

def make_assign(span: span.Span, left: *Expr, right: *Expr): *Expr {
    return (new AssignExpr {
        e: Expr { tag: EXPR_ASSIGN, span: span },
        left: left,
        right: right
    }) as *Expr;
}

def is_assign(e: *Expr): bool {
    return e.tag == EXPR_ASSIGN;
}

def as_assign(e: *Expr): *AssignExpr {
    return e as *AssignExpr;
}

/////////// ident expr ///////////

type IdentExpr struct {
    e: Expr,
    ident: ident.Ident
}

def make_ident(ident: lexer.Token): *Expr {
    return (new IdentExpr {
        e: Expr { tag: EXPR_IDENT, span: ident.span },
        ident: ident.create(ident)
    }) as *Expr;
}

def is_ident(e: *Expr): bool {
    return e.tag == EXPR_IDENT;
}

def as_ident(e: *Expr): *IdentExpr {
    return e as *IdentExpr;
}

/////////// int expr ///////////

// TODO: hex, oct ...
type IntExpr struct {
    e: Expr,
    ptr: string
}

def make_int(span: span.Span, ptr: string): *Expr {
    return (new IntExpr {
        e: Expr { tag: EXPR_INT, span: span },
        ptr: ptr
    }) as *Expr;
}

def is_int(e: *Expr): bool {
    return e.tag == EXPR_INT;
}

def as_int(e: *Expr): *IntExpr {
    return e as *IntExpr;
}

/////////// string expr ///////////

type StringExpr struct {
    e: Expr,
    ptr: string
}

def make_string(span: span.Span, ptr: string): *Expr {
    return (new StringExpr {
        e: Expr { tag: EXPR_STRING, span: span },
        ptr: ptr
    }) as *Expr;
}

def is_string(e: *Expr): bool {
    return e.tag == EXPR_STRING;
}

def as_string(e: *Expr): *StringExpr {
    return e as *StringExpr;
}


/////////// binary expr ///////////

type BinaryExpr struct {
    e: Expr,
    left: *Expr,
    right: *Expr,
    ty: i32
}

def make_binary(span: span.Span, ty: i32, left: *Expr, right: *Expr): *Expr {
    return (new BinaryExpr {
        e: Expr { tag: EXPR_BINARY, span: span },
        left: left,
        right: right,
        ty: ty
    }) as *Expr;
}

def is_binary(e: *Expr): bool {
    return e.tag == EXPR_BINARY;
}

def as_binary(e: *Expr): *BinaryExpr {
    return e as *BinaryExpr;
}

/////////// unaryexpr ///////////

type UnaryExpr struct {
    e: Expr,
    ty: i32,
    right: *Expr
}

def make_unary(span: span.Span, ty: i32, right: *Expr): *Expr {
    return (new UnaryExpr {
        e: Expr { tag: EXPR_UNARY, span: span },
        ty: ty,
        right: right
    }) as *Expr;
}

def is_unary(e: *Expr): bool {
    return e.tag == EXPR_UNARY;
}

def as_unary(e: *Expr): *UnaryExpr {
    return e as *UnaryExpr;
}

/////////// base expr ///////////

type Expr struct {
    tag: i32,
    span: span.Span
}

def expr_free(e: *Expr): void {
    if e == null {
        return;
    }

    if is_binary(e) {
        let b_expr = as_binary(e);
        expr_free(b_expr.left);
        expr_free(b_expr.right);
    } else if is_assign(e) {
        let a_expr = as_assign(e);
        expr_free(a_expr.left);
        expr_free(a_expr.right);
    } else if is_access(e) {
        let a_expr = as_access(e);
        expr_free(a_expr.left);
        expr_free(a_expr.right);
    } else if is_call(e) {
        let c_expr = as_call(e);
        expr_free(c_expr.ident as *Expr);
        free_al(&c_expr.args);
    } else if is_init(e) {
        let i_expr = as_init(e);
        expr_free(i_expr.ident as *Expr);
        free_il(&i_expr.inits);
    } else if is_unary(e) {
        expr_free(as_unary(e).right);
    }

    delete e;
}

def expr_to_string(e: *Expr, span_interner: *span.SpanInterner): string {
    if is_int(e) {
        let i_expr = as_int(e);
        let len = span.get(span_interner, e.span).len;
        return std.strndup(i_expr.ptr, len);
    }

    if is_string(e) {
        let s_expr = as_string(e);
        let len = span.get(span_interner, e.span).len;
        // include the quotes
        return std.strndup(s_expr.ptr - 1, len + 2);
    }

    if is_ident(e) {
        let i_expr = as_ident(e);
        return ident.to_string(&i_expr.ident, span_interner);
    }

    if is_assign(e) {
        let a_expr = as_assign(e);

        let left = expr_to_string(a_expr.left, span_interner);
        let right = expr_to_string(a_expr.right, span_interner);

        let s = std.format_str("%s = %s", left, right);
        delete left;
        delete right;

        return s;
    }

    if is_access(e) {
        let a_expr = as_access(e);

        let left = expr_to_string(a_expr.left, span_interner);
        let right = expr_to_string(a_expr.right, span_interner);

        let s = std.format_str("%s.%s", left, right);
        delete left;
        delete right;

        return s;
    }

    if is_unary(e) {
        let u_expr = as_unary(e);
        let right = expr_to_string(u_expr.right, span_interner);
        let tok_str = unary2str(u_expr.ty);

        let s = std.format_str("(%s%s)", tok_str, right);
        delete right;

        return s;
    }

    if is_binary(e) {
        let b_expr = as_binary(e);

        let left = expr_to_string(b_expr.left, span_interner);
        let right = expr_to_string(b_expr.right, span_interner);
        let tok_str = binary2str(b_expr.ty);

        let s = std.format_str("(%s%s%s)", left, tok_str, right);
        delete left;
        delete right;

        return s;
    }

    if is_call(e) {
        let c_expr = as_call(e);

        let ident = expr_to_string(c_expr.ident as *Expr, span_interner);
        let args = arg_list_to_string(&c_expr.args, span_interner);

        let s = std.format_str("%s(%s)", ident, args);
        delete ident;
        delete args;

        return s;
    }

    if is_init(e) {
        let i_expr = as_init(e);

        let ident = expr_to_string(i_expr.ident as *Expr, span_interner);
        let inits = init_list_to_string(&i_expr.inits, span_interner);

        let s = std.format_str("%s{%s}", ident, inits);
        delete ident;
        delete inits;

        return s;
    }

    return null;
}

/////////// ArgList ///////////
// TODO: should be in own file, but can't because compiler can't handle circular imports

type ArgList struct {
    args: ptrvec.Vec
}

def create_arg_list(): ArgList {
    return ArgList { args: ptrvec.create() };
}

def get_arg_at(al: *ArgList, i: i32): *ast.Expr {
    return ptrvec.get(&al.args, i) as *ast.Expr;
}

def push_arg(al: *ArgList, arg: *ast.Expr): void {
    ptrvec.push_ptr(&al.args, arg as *void);
}

def free_al(al: *ArgList): void {
    let i = 0;
    while i < al.args.len {
        let e = ptrvec.get(&al.args, i) as *ast.Expr;
        ast.expr_free(e);
        i = i + 1;
    }

    ptrvec.free_v(&al.args);
}

def arg_list_to_string(al: *ArgList, span_interner: *span.SpanInterner): string {
    let args = (new '\0') as string;

    let i = 0;
    while i < al.args.len {
        let a = get_arg_at(al, i);
        let s = expr_to_string(a, span_interner);

        let comma_len = 0;
        if i > 0 {
            comma_len = 2;
        }

        let new_len = std.strlen(args) + std.strlen(s) + 1 + comma_len;
        let new_args = std.malloc(new_len) as string;
        let old_args = args;
        args = std.strcpy(new_args, args);
        delete old_args;

        if comma_len > 0 {
            args = std.strcat(args, ", ");
        }

        args = std.strcat(args, s);
        delete s;

        i = i + 1;
    }

    return args;
}

/////////// InitList ///////////
// TODO: should be in own file, but can't because compiler can'e handle circular imports

type Init struct {
    ident: ident.Ident,
    expr: *Expr
}

def create_init(ident: ident.Ident, expr: *Expr): Init {
    return Init { ident: ident, expr: expr };
}

def empty_init(): Init {
    return Init { ident: ident.empty(), expr: null };
}

type InitList struct {
    inits: vec.Vec
}

def create_init_list(): InitList {
    return InitList { inits: vec.create(sizeof Init) };
}

def get_init_at(il: *InitList, i: i32, dest: *Init): bool {
    return vec.get(&il.inits, i, dest as *void);
}

def push_init(il: *InitList, init: Init): void {
    vec.push(&il.inits, &init as *void);
}

def free_il(il: *InitList): void {
    let i = 0;
    while i < il.inits.len {
        let init = empty_init();
        get_init_at(il, i, &init);
        ast.expr_free(init.expr);
        i = i + 1;
    }

    vec.free_v(&il.inits);
}

def init_list_to_string(il: *InitList, span_interner: *span.SpanInterner): string {
    let inits = (new '\0') as string;

    let i = 0;
    while i < il.inits.len {
        let init = empty_init();
        if !get_init_at(il, i, &init) {
            delete inits;
            return null;
        }
        let n = ident.to_string(&init.ident, span_interner);
        let e = expr_to_string(init.expr, span_interner);

        let extra_len = 2; // ': '
        if i > 0 {
            extra_len = 4; // ': ..., '
        }

        let new_len = std.strlen(inits) + std.strlen(n) + std.strlen(e) + 1 + extra_len;
        let new_inits = std.malloc(new_len) as string;
        let old_inits = inits;
        inits = std.strcpy(new_inits, inits);
        delete old_inits;

        if extra_len > 2 {
            inits = std.strcat(inits, ", ");
        }

        inits = std.strcat(inits, n);
        inits = std.strcat(inits, ": ");
        inits = std.strcat(inits, e);
        delete n;
        delete e;

        i = i + 1;
    }

    return inits;
}
