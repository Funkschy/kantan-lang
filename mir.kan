#include "mir_types.h"
#include "std.h"

import "ty";
import "std";
import "vec";
import "str";
import "map";
import "span";
import "source";

extern def mir_binary2str(kind: i32): string;
extern def mir_unary2str(kind: i32): string;

type Module struct {
    globals: map.Map, // map.Map<Name, GlobalConst>
    instructions: InstBlock
}

def create_mod(globals: map.Map, instructions: InstBlock): Module {
    return Module {
        globals: globals,
        instructions: instructions
    };
}

type MetaData struct {
    pos: source.SourcePos
}

def create_meta(pos: source.SourcePos): MetaData {
    return MetaData { pos: pos };
}

type GlobalData struct {
    src_pos: span.Span,
    ty: *ty.Type
}

def create_global_data(src_pos: span.Span, ty: *ty.Type): GlobalData {
    return GlobalData {
        src_pos: src_pos,
        ty: ty
    };
}

type GlobalConst struct {
    name: Name,
    data: GlobalData
}

def create_const(name: Name, src_pos: span.Span, ty: *ty.Type): GlobalConst {
    return GlobalConst {
        name: name,
        data: create_global_data(src_pos, ty)
    };
}

def const_name(c: *GlobalConst): Name {
    return ref_name(c.name.len, c.name.data);
}

type InstBlock struct {
    instructions: vec.Vec // vec.Vec<Inst>
}

def create_inst_block(): InstBlock {
    return InstBlock { instructions: vec.create(sizeof Inst) };
}

def add_instr(b: *InstBlock, inst: Inst): void {
    vec.push(&b.instructions, &inst as *void);
}

type BasicBlock struct {
    label: Label,
    instructions: vec.Vec, // vec.Vec<Inst>
    terminator: Inst
}

def create_bb(label: Label): BasicBlock {
    return BasicBlock {
        label: label,
        instructions: vec.create(sizeof Inst),
        terminator: create_inst_nop()
    };
}

type Name struct {
    owned: bool,
    len: i32,
    data: string
}

def make_owned_name(len: i32, data: string): Name {
    return Name { owned: true, len: len, data: data };
}

def ref_name(len: i32, data: string): Name {
    return Name { owned: false, len: len, data: data };
}

def free_name(n: *Name): void {
    if n.owned {
        delete n.data;
    }
}

type Label struct {
    name: Name
}

def label_name(l: *Label): Name {
    return ref_name(l.name.len, l.name.data);
}

type AddressName struct {
    value: Name
}

def as_addr_name(a: *Address): *AddressName {
    return a.data as *AddressName;
}

def is_addr_name(a: *Address): bool {
    return a.kind == MIR_ADDR_NAME;
}

type AddressConst struct {
    span: span.Span
}

def create_addr_const(ty: *ty.Type, s: span.Span): Address {
    let cnst = (new AddressConst { span: s }) as *void;
    return create_addr(MIR_ADDR_CONST, ty, cnst);
}

def as_addr_const(a: *Address): *AddressConst {
    return a.data as *AddressConst;
}

def is_addr_const(a: *Address): bool {
    return a.kind == MIR_ADDR_CONST;
}

type AddressTemp struct {
    value: i32
}

def as_addr_temp(a: *Address): *AddressTemp {
    return a.data as *AddressTemp;
}

def is_addr_temp(a: *Address): bool {
    return a.kind == MIR_ADDR_TEMP;
}

type AddressGlobal struct {
    name: Name
}

def new_address_global(name: Name, ty: *ty.Type): Address {
    return Address {
        kind: MIR_ADDR_GLOBAL,
        ty: ty,
        data: (new AddressGlobal { name: name }) as *void
    };
}

def as_addr_global(a: *Address): *AddressGlobal {
    return a.data as *AddressGlobal;
}

def is_addr_global(a: *Address): bool {
    return a.kind == MIR_ADDR_GLOBAL;
}

type AddressRef struct {
    name: Name
}

def as_addr_ref(a: *Address): *AddressRef {
    return a.data as *AddressRef;
}

def is_addr_ref(a: *Address): bool {
    return a.kind == MIR_ADDR_REF;
}

def create_addr_empty(): Address {
    return create_addr(MIR_ADDR_EMPTY, null, null);
}

def is_addr_empty(a: *Address): bool {
    return a.kind == MIR_ADDR_EMPTY;
}

def create_addr_null(ty: *ty.Type): Address {
    return create_addr(MIR_ADDR_NULL, ty, null);
}

def is_addr_null(a: *Address): bool {
    return a.kind == MIR_ADDR_NULL;
}

type Address struct {
    kind: i32,
    ty: *ty.Type,
    data: *void
}

def create_addr(kind: i32, ty: *ty.Type, data: *void): Address {
    return Address {
        kind: kind, ty: ty, data: data
    };
}

def free_add(a: *Address): void {
    if a.data != null {
        delete a.data;
    }
}

def addr_to_string(a: *Address): string {
    if is_addr_name(a) {
        let name = as_addr_name(a);
        return std.strndup(name.value.data, name.value.len);
    }

    if is_addr_const(a) {
        let const = as_addr_const(a);
        return std.strndup(const.span.start, span.len(const.span));
    }

    if is_addr_temp(a) {
        let tmp = as_addr_temp(a);
        return std.format_str("_t%d", tmp.value);
    }

    if is_addr_global(a) {
        let global = as_addr_global(a);
        return std.strndup(global.name.data, global.name.len);
    }

    if is_addr_ref(a) {
        let ref = as_addr_ref(a);
        return std.format_str("&%.*s", ref.name.len, ref.name.data);
    }

    if is_addr_empty(a) {
        return std.strdup("_");
    }

    if is_addr_null(a) {
        return std.strdup("null");
    }

    return null;
}

type ExprBinary struct {
    kind: i32,
    left: Address,
    right: Address
}

def binary_kind_to_static_string(kind: i32): string {
    return mir_binary2str(kind);
}

def as_expr_binary(e: *Expression): *ExprBinary {
    return e.data as *ExprBinary;
}

def is_expr_binary(e: *Expression): bool {
    return e.kind == MIR_EXPR_BINARY;
}

type ExprUnary struct {
    kind: i32,
    addr: Address
}

def unary_kind_to_static_string(kind: i32): string {
    return mir_unary2str(kind);
}

def as_expr_unary(e: *Expression): *ExprUnary {
    return e.data as *ExprUnary;
}

def is_expr_unary(e: *Expression): bool {
    return e.kind == MIR_EXPR_UNARY;
}

type ExprCopy struct {
    addr: Address
}

def as_expr_copy(e: *Expression): *ExprCopy {
    return e.data as *ExprCopy;
}

def is_expr_copy(e: *Expression): bool {
    return e.kind == MIR_EXPR_COPY;
}

type ExprCall struct {
    name: Name,
    args: vec.Vec // Vec<Address>
}

def call_arg_at(call: *ExprCall, idx: i32): *Address {
    return vec.get_ptr(&call.args, idx) as *Address;
}

def as_expr_call(e: *Expression): *ExprCall {
    return e.data as *ExprCall;
}

def is_expr_call(e: *Expression): bool {
    return e.kind == MIR_EXPR_CALL;
}

type ExprStructGep struct {
    addr: Address,
    idx: i32
}

def as_expr_struct_gep(e: *Expression): *ExprStructGep {
    return e.data as *ExprStructGep;
}

def is_expr_struct_gep(e: *Expression): bool {
    return e.kind == MIR_EXPR_STRUCTGEP;
}

type ExprStructInit struct {
    name: Name,
    args: vec.Vec // Vec<Address>
}

def init_arg_at(init: *ExprStructInit, idx: i32): *Address {
    return vec.get_ptr(&init.args, idx) as *Address;
}

def as_expr_struct_init(e: *Expression): *ExprStructInit {
    return e.data as *ExprStructInit;
}

def is_expr_struct_init(e: *Expression): bool {
    return e.kind == MIR_EXPR_STRUCTINIT;
}

type ExprNew struct {
    addr: Address
}

def as_expr_new(e: *Expression): *ExprNew {
    return e.data as *ExprNew;
}

def is_expr_new(e: *Expression): bool {
    return e.kind == MIR_EXPR_NEW;
}

type ExprSizeOf struct {
    addr: Address
}

def as_expr_sizeof(e: *Expression): *ExprSizeOf {
    return e.data as *ExprSizeOf;
}

def is_expr_sizeof(e: *Expression): bool {
    return e.kind == MIR_EXPR_SIZEOF;
}

type ExprGetParam struct {
    idx: i32
}

def as_expr_getparam(e: *Expression): *ExprGetParam {
    return e.data as *ExprGetParam;
}

def is_expr_getparam(e: *Expression): bool {
    return e.kind == MIR_EXPR_GETPARAM;
}

type ExprBitCast struct {
    addr: Address,
    into: *ty.Type
}

def as_expr_bitcast(e: *Expression): *ExprBitCast {
    return e.data as *ExprBitCast;
}

def is_expr_bitcast(e: *Expression): bool {
    return e.kind == MIR_EXPR_BITCAST;
}

// An expression is always on the right side of an assignment instruction
// In the comments, this assignment is denoted as 'x = '
//
// x = y op z:
//  Binary(Address, BinaryType, Address)
// x = op y
//  Unary(UnaryType, Address),
// x = y:
//  Copy(Address),
// x = call f (y, z):
//  Call(Name, Vec<Address>, return: Type, varargs: bool)
// Gets a pointer to the Xth element of a struct or array
// x = base + offset:
//  StructGep(Address, u32)
// x = test { 41, "test" }:
//  StructInit(UserIdent, Vec<Address>),
// allocates the value of its address on the heap
// x = new 5:
//  New(Address, Type),
// x = sizeof(ty):
//  SizeOf(Type),
// x = param #y:
//  GetParam(u32),
// Cast the value at the address to the supplied type
// x = bitcast e to ty2:
//  BitCast(Address, Type),
type Expression struct {
    kind: i32,
    data: *void
}

def expr_to_string(e: *Expression): string {
    if is_expr_binary(e) {
        let bin = as_expr_binary(e);

        let left = addr_to_string(&bin.left);
        let right = addr_to_string(&bin.right);
        let op = binary_kind_to_static_string(bin.kind);

        let s = std.format_str("%s%s%s", left, op, right);

        delete left;
        delete right;
        return s;
    }

    if is_expr_unary(e) {
        let unary = as_expr_unary(e);

        let addr = addr_to_string(&unary.addr);
        let op = unary_kind_to_static_string(unary.kind);

        let s = std.format_str("%s%s", op, addr);

        delete addr;
        return s;
    }

    if is_expr_copy(e) {
        let copy = as_expr_copy(e);

        let addr = addr_to_string(&copy.addr);
        let s = std.format_str("copy %s", addr);

        delete addr;
        return s;
    }

    if is_expr_call(e) {
        let call = as_expr_call(e);

        let buf = str.create_buf();

        let i = 0;
        while i < call.args.len {
            let arg = call_arg_at(call, i);
            let arg_s = addr_to_string(arg);

            str.append(&buf, arg_s);
            if i < call.args.len - 1 {
                str.append(&buf, ", ");
            }

            delete arg_s;
            i = i + 1;
        }

        let name = call.name;
        let s = std.format_str("%.*s(%s)", name.len, name.data, buf.content);

        str.free_buf(&buf);

        return s;
    }

    if is_expr_struct_gep(e) {
        let gep = as_expr_struct_gep(e);
        let addr_s = addr_to_string(&gep.addr);

        let s = std.format_str("gep(%s, %d)", addr_s, gep.idx);

        delete addr_s;
        return s;
    }

    if is_expr_struct_init(e) {
        let init = as_expr_struct_init(e);

        let buf = str.create_buf();

        let i = 0;
        while i < init.args.len {
            let arg = init_arg_at(init, i);
            let arg_s = addr_to_string(arg);

            str.append(&buf, arg_s);
            if i < init.args.len - 1 {
                str.append(&buf, ", ");
            }

            delete arg_s;
            i = i + 1;
        }

        let name = init.name;
        let s = std.format_str("%.*s{%s}", name.len, name.data, buf.content);

        str.free_buf(&buf);

        return s;
        return s;
    }

    if is_expr_new(e) {
        let n = as_expr_new(e);
        let addr_s = addr_to_string(&n.addr);

        let s = std.format_str("new %s", addr_s);

        delete addr_s;
        return s;
    }

    if is_expr_sizeof(e) {
        let so = as_expr_sizeof(e);
        let addr_s = addr_to_string(&so.addr);

        let s = std.format_str("sizeof(%s)", addr_s);

        delete addr_s;
        return s;
    }

    if is_expr_getparam(e) {
        let gp = as_expr_getparam(e);
        return std.format_str("getparam(%d)", gp.idx);
    }

    if is_expr_bitcast(e) {
        let cast = as_expr_bitcast(e);
        let addr_s = addr_to_string(&cast.addr);
        let ty_s: string = null;
        let free_ty_s = ty.to_string(cast.into, &ty_s);

        let s = std.format_str("(%s as %s)", addr_s, ty_s);

        delete addr_s;
        if free_ty_s {
            delete ty_s;
        }

        return s;
    }

    return null;
}

type InstDecl struct {
    name: Name,
    ty: *ty.Type
}

def new_inst_decl(meta: MetaData, name: Name, ty: *ty.Type): Inst {
    return Inst {
        kind: MIR_INST_DECL,
        meta: meta,
        data: (new InstDecl {name: name, ty: ty}) as *void
    };
}

def is_inst_decl(inst: *Inst): bool {
    return inst.kind == MIR_INST_DECL;
}

def as_inst_decl(inst: *Inst): *InstDecl {
    return inst.data as *InstDecl;
}

type InstAssign struct {
    dest: Address,
    value: Expression
}

def is_inst_assign(inst: *Inst): bool {
    return inst.kind == MIR_INST_ASSIGN;
}

def as_inst_assign(inst: *Inst): *InstAssign {
    return inst.data as *InstAssign;
}

type InstJmp struct {
    target: Label
}

def is_inst_jmp(inst: *Inst): bool {
    return inst.kind == MIR_INST_JMP;
}

def as_inst_jmp(inst: *Inst): *InstJmp {
    return inst.data as *InstJmp;
}

type InstJmpIf struct {
    condition: Address,
    if_target: Label,
    else_target: Label
}

def is_inst_jmpif(inst: *Inst): bool {
    return inst.kind == MIR_INST_JMPIF;
}

def as_inst_jmpif(inst: *Inst): *InstJmpIf {
    return inst.data as *InstJmpIf;
}

type InstReturn struct {
    value: Address // MIR_ADDR_EMPTY if empty return: 'return;'
}

def is_inst_return(inst: *Inst): bool {
    return inst.kind == MIR_INST_RETURN;
}

def as_inst_return(inst: *Inst): *InstReturn {
    return inst.data as *InstReturn;
}

type InstLabel struct {
    value: Label
}

def is_inst_label(inst: *Inst): bool {
    return inst.kind == MIR_INST_LABEL;
}

def as_inst_label(inst: *Inst): *InstLabel {
    return inst.data as *InstLabel;
}

def is_inst_delete(inst: *Inst): bool {
    return inst.kind == MIR_INST_DELETE;
}

def as_inst_delete(inst: *Inst): *InstDelete {
    return inst.data as *InstDelete;
}

type InstDelete struct {
    addr: Address
}

def create_inst_nop(): Inst {
    return Inst {
        kind: MIR_INST_NOP,
        meta: create_meta(source.empty_pos()),
        data: null
    };
}

def is_inst_nop(inst: *Inst): bool {
    return inst.kind == MIR_INST_NOP;
}

// A single MIR instruction
//
// can be one of the following
// let x: i32:
//  Decl(Address, Type),
// x = <expr>:
//  Assignment(Address, Expr),
// goto l:
//  Jmp(Label),
// if x goto l0 else goto l1:
//  JmpIf(Address, Label, Label),
// return x:
//  Return(Optional Address),
// .L0:
//  Label(Label),
// frees heap memory:
//  Delete(Address),
// No operation:
//  Nop,
type Inst struct {
    kind: i32,
    meta: MetaData,
    data: *void
}

def inst_to_string(inst: *Inst): string {
    if is_inst_decl(inst) {
        let decl = as_inst_decl(inst);
        let ty_s: string = null;
        let free_ty_s = ty.to_string(decl.ty, &ty_s);

        let s = std.format_str(
            "let %.*s: %s",
            decl.name.len,
            decl.name.data

        );

        if free_ty_s {
            delete ty_s;
        }

        return s;
    }

    if is_inst_assign(inst) {
        let assign = as_inst_assign(inst);
        let a_s = addr_to_string(&assign.dest);
        let e_s = expr_to_string(&assign.value);

        let s = std.format_str("%s = %s", a_s, e_s);

        delete a_s;
        delete e_s;

        return s;
    }

    if is_inst_jmp(inst) {
        let jmp = as_inst_jmp(inst);
        let label = label_name(&jmp.target);

        // just copy the data, since this function is for debug only
        return std.strndup(label.data, label.len);
    }

    if is_inst_jmpif(inst) {
        let jmpif = as_inst_jmpif(inst);
        let if_label = label_name(&jmpif.if_target);
        let else_label = label_name(&jmpif.else_target);
        let condition = addr_to_string(&jmpif.condition);

        let s = std.format_str("jmp %s;%.*s;%.*s", condition, if_label, else_label);
        delete condition;

        return s;
    }

    if is_inst_return(inst) {
        let ret = as_inst_return(inst);
        if is_addr_empty(&ret.value) {
            return std.strdup("return");
        }

        let a_s = addr_to_string(&ret.value);
        let s = std.format_str("return %s", a_s);

        delete a_s;
        return s;
    }

    if is_inst_label(inst) {
        let label = as_inst_label(inst);
        let name = label_name(&label.value);
        return std.format_str("%.*s:", name.len, name.data);
    }

    if is_inst_delete(inst) {
        let del = as_inst_delete(inst);
        let a_s = addr_to_string(&del.addr);

        let s = std.format_str("delete %s", a_s);

        delete a_s;
        return s;
    }

    if is_inst_nop(inst) {
        return std.strdup("nop");
    }

    return null;
}

def free_inst(inst: *Inst): void {
    if inst.data != null {
        delete inst.data;
    }
}
