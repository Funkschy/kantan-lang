#include "std.h"

import "io";
import "ty";
import "nt";
import "vec";
import "mod";
import "mir";
import "ast";
import "map";
import "std";
import "dbg";
import "span";
import "name";
import "path";
import "scope";
import "ident";
import "source";

// Data which is shared between all the MirCompiler instances
type CompilationData struct {
    mods: *mod.Map,
    proj_root: path.Path,
    global_types: map.Map // map.Map<ident.Ident, *ty.Type>
}

// Compiles a single *mod.Module to a mir.Module
type MirCompiler struct {
    label_count: i32,
    mod: *mod.Module,
    data: *CompilationData,
    names: nt.NameTable,
    functions: map.Map, // map.Map<name.Ref, *mir.Func>
    structs: map.Map, // map.Map<name.Ref, *mir.Struct>
    globals: map.Map, // map.Map<name.Ref, *mir.GlobalConst>
    current_func: *mir.Func
}

def create_mirc(m: *mod.Module, data: *CompilationData): MirCompiler {
    return MirCompiler {
        label_count: 0,
        mod: m,
        data: data,
        names: nt.create(),
        functions: map.create(),
        structs: map.create(),
        globals: map.create(),
        current_func: null
    };
}

def free_compiler(c: *MirCompiler): void {
    nt.free_nt(&c.names);
}

def compile(c: *MirCompiler): mir.Module {
    let i = 0;
    let n = mod.num_stmts(c.mod);

    nt.open_scope(&c.names);

    while i < n {
        let s = mod.get_stmt_at(c.mod, i);
        compile_stmt(c, s);
        i = i + 1;
    }

    nt.close_scope(&c.names);

    return mir.create_mod(c.globals, c.functions, c.structs, c.mod);
}

def add_function(c: *MirCompiler, f: *mir.Func): void {
    let key = map.create_key(f.name.len, f.name.data);
    map.insert(&c.functions, key, f as *void);
}

def add_struct(c: *MirCompiler, s: *mir.Struct): void {
    let key = map.create_key(s.name.len, s.name.data);
    map.insert(&c.structs, key, s as *void);
}

def lookup_struct(c: *MirCompiler, mangled_name: name.Ref): *mir.Struct {
    let key = map.create_key(mangled_name.len, mangled_name.data);
    return map.get(&c.structs, key) as *mir.Struct;
}

def lookup_function(c: *MirCompiler, mangled_name: name.Ref): *mir.Func {
    let key = map.create_key(mangled_name.len, mangled_name.data);
    return map.get(&c.functions, key) as *mir.Func;
}

def get_meta(c: *MirCompiler, s: span.Span): mir.MetaData {
    let pos = source.span_to_pos(c.mod.source, s);
    return mir.create_meta(pos);
}

def lookup_or_declare_struct(c: *MirCompiler, t: *ty.Type): *mir.Struct {
    dbg.assert(ty.is_struct(t), "non struct type passed to lookup");

    let s_ty = ty.as_struct(t);
    let mangled_name = name.ref(s_ty.mangled_name);
    let mir_struct = lookup_struct(c, mangled_name);

    if mir_struct == null {
        let i = 0;
        while i < ty.num_fields(s_ty) {
            let field = ty.field_at(s_ty, i);
            if ty.is_struct(field.ty) {
                lookup_or_declare_struct(c, field.ty);
            }
            i = i + 1;
        }

        let is_extern = s_ty.declared_in != c.mod.idx;
        mir_struct = new mir.create_struct(t, mangled_name, is_extern);
        add_struct(c, mir_struct);
    }

    return mir_struct;
}

def lookup_or_declare_function(c: *MirCompiler, t: *ty.Type): *mir.Func {
    dbg.assert(ty.is_func(t), "non function type passed to lookup");

    let f_ty = ty.as_func(t);
    let mangled_name = name.ref(f_ty.mangled_name);
    let mir_func = lookup_function(c, mangled_name);

    if mir_func == null {
        let is_extern = f_ty.declared_in != c.mod.idx || f_ty.is_extern;
        mir_func = new mir.create_func(t, mangled_name, is_extern);
        add_function(c, mir_func);
    }

    return mir_func;
}

def compile_stmt(c: *MirCompiler, s: *ast.Stmt): void {
    if ast.is_struct_decl_stmt(s) {
        let decl_stmt = ast.as_struct_decl_stmt(s);
        let decl = &decl_stmt.decl;

        lookup_or_declare_struct(c, decl.struct_ty);
        return;
    }

    if ast.is_func_decl_stmt(s) {
        let decl_stmt = ast.as_func_decl_stmt(s);
        let decl = &decl_stmt.decl;

        let mir_func = lookup_or_declare_function(c, decl.func_ty);

        if decl_stmt.block != null {
            let prev_func = c.current_func;

            c.current_func = mir_func;
            nt.open_scope(&c.names);

            // Declare arguments
            let i = 0;
            let f_ty = ty.as_func(decl.func_ty);
            let n = f_ty.params.len;
            while i < n {
                let p = ty.param_at(f_ty, i);
                let meta = get_meta(c, p.name.span);

                let name = bind_ident(c, &p.name);
                push_decl(c, name, p.ty, meta);

                let value = mir.new_expr_copy(mir.make_addr_arg(i, p.ty));
                let meta = get_meta(c, p.name.span);
                let target = mir.make_addr_name(name, p.ty);
                push_assign(c, target, value, meta);

                i = i + 1;
            }

            // compile function code
            compile_stmt(c, decl_stmt.block as *ast.Stmt);
            c.current_func = prev_func;
            nt.close_scope(&c.names);
        }

        return;
    }

    if ast.is_let_stmt(s) {
        let let_stmt = ast.as_let_stmt(s);
        let ty = let_stmt.value.ty;

        let a = mir.create_addr_empty();
        let mir_expr = mir.empty_expr();

        if address_expression(c, let_stmt.value, &a) {
            mir_expr = mir.new_expr_copy(a);
        } else {
            mir_expr = complex_expr(c, let_stmt.value, true);
        }

        let name = bind_ident(c, &let_stmt.ident);

        // TODO(#73): implement logic for global variables
        if c.current_func != null {
            let meta = get_meta(c, let_stmt.ident.span);
            push_decl(c, name, ty, meta);
        } else {
            mir.free_expr(&mir_expr);
            return;
        }

        let meta = get_meta(c, let_stmt.ident.span);
        let target = mir.make_addr_name(name, ty);
        push_assign(c, target, mir_expr, meta);

        return;
    }

    if ast.is_block_stmt(s) {
        dbg.assert(c.current_func != null, "current_func was null");

        let block_stmt = ast.as_block_stmt(s);
        bind_s(c, 5, "block");

        nt.open_scope(&c.names);

        let i = 0;
        while i < block_stmt.stmts.len {
            let s = ast.stmt_at(block_stmt, i);
            compile_stmt(c, s);
            i = i + 1;
        }

        nt.close_scope(&c.names);

        return;
    }

    if ast.is_expr_stmt(s) {
        let e = ast.as_expr_stmt(s);
        expr_instruction(c, e.expr, false);
        return;
    }

    if ast.is_delete_stmt(s) {
        let target = expr_instruction(c, ast.as_delete_stmt(s).expr, false);
        let meta = get_meta(c, s.span);
        push_instr(c, mir.new_inst_delete(meta, target));
        return;
    }

    if ast.is_if_stmt(s) {
        let end_label = label(c);
        push_if_stmt(c, s, &end_label);
        // TODO: wrong meta for if-stmt-endlabel
        let end_meta = get_meta(c, ast.as_if_stmt(s).block.s.span);
        push_instr(c, mir.new_inst_label(end_meta, end_label));
        return;
    }

    if ast.is_while_stmt(s) {
        let end_label = label(c);
        push_while_stmt(c, s, &end_label);
        // TODO: wrong meta for while-stmt-endlabel
        let end_meta = get_meta(c, ast.as_while_stmt(s).block.s.span);
        push_instr(c, mir.new_inst_label(end_meta, end_label));
        return;
    }

    if ast.is_return_stmt(s) {
        let ret = ast.as_return_stmt(s);
        let meta = get_meta(c, s.span);

        if ret.expr == null {
            push_instr(c, mir.new_inst_return(meta));
            return;
        }

        let value = expr_instruction(c, ret.expr, true);
        push_instr(c, mir.new_inst_return_value(meta, value));
        return;
    }
}

def push_decl(c: *MirCompiler, n: name.Ref, ty: *ty.Type, m: mir.MetaData): void {
    push_instr(c, mir.new_inst_decl(m, n, ty));
}

def push_instr_p(c: *MirCompiler, instr: *mir.Inst): void {
    mir.func_push_instr(c.current_func, instr);
}

def push_instr(c: *MirCompiler, instr: mir.Inst): void {
    mir.func_push_instr(c.current_func, &instr);
}

def push_assign(
    c: *MirCompiler,
    target: mir.Address,
    value: mir.Expression,
    m: mir.MetaData
): void {
    let assign = mir.new_inst_assign(m, target, value, target.ty);
    push_instr_p(c, &assign);
}

def push_temp_assign(
    c: *MirCompiler,
    value: mir.Expression,
    ty: *ty.Type,
    meta: mir.MetaData,
    out_name: *mir.Address // has to be freed if wanted
): void {
    let target = temp_addr(c, ty);
    let assign = mir.new_inst_assign(meta, target, value, target.ty);
    push_instr_p(c, &assign);

    if out_name != null {
        *out_name = mir.clone_addr_name(&target);
    }
}

def push_if_stmt(c: *MirCompiler, s: *ast.Stmt, end_label: *mir.Label): void {
    let if_stmt = ast.as_if_stmt(s);
    let condition = expr_instruction(c, if_stmt.condition, true);
    let has_else = if_stmt.else_stmt != null;
    let has_else_if = false;
    if has_else {
        has_else_if = ast.is_if_stmt(if_stmt.else_stmt);
    }

    let else_label = mir.create_label(name.make_owned(0, null));
    if has_else {
        else_label = label(c);
    } else {
        else_label = mir.clone_label(end_label);
    }

    let then_label = label(c);
    let cond_jmp = mir.new_inst_jmpif(
        get_meta(c, s.span),
        condition,
        mir.clone_label(&then_label),
        mir.clone_label(&else_label)
    );
    push_instr(c, cond_jmp);

    let then_meta = get_meta(c, if_stmt.block.s.span);
    push_instr(c, mir.new_inst_label(then_meta, then_label));

    compile_stmt(c, if_stmt.block as *ast.Stmt);
    let end_jmp = mir.new_inst_jmp(get_meta(c, s.span), mir.clone_label(end_label));
    push_instr(c, end_jmp);

    if has_else {
        let else_meta = get_meta(c, if_stmt.else_stmt.span);
        push_instr(c, mir.new_inst_label(else_meta, else_label));

        if has_else_if {
            push_if_stmt(c, if_stmt.else_stmt, end_label);
        } else {
            compile_stmt(c, if_stmt.else_stmt);
        }
    } else {
        mir.free_label(&else_label);
    }
}

def push_while_stmt(c: *MirCompiler, s: *ast.Stmt, end_label: *mir.Label): void {
    let while_stmt = ast.as_while_stmt(s);

    let condition_label = label(c);
    let condition_meta = get_meta(c, while_stmt.condition.span);
    let condition_label_clone = mir.clone_label(&condition_label);
    push_instr(c, mir.new_inst_label(condition_meta, condition_label_clone));

    let condition = expr_instruction(c, while_stmt.condition, true);

    let then_label = label(c);
    let cond_jmp = mir.new_inst_jmpif(
        get_meta(c, s.span),
        condition,
        mir.clone_label(&then_label),
        mir.clone_label(end_label)
    );
    push_instr(c, cond_jmp);

    let then_meta = get_meta(c, while_stmt.block.s.span);
    push_instr(c, mir.new_inst_label(then_meta, then_label));

    compile_stmt(c, while_stmt.block as *ast.Stmt);
    push_instr(c, mir.new_inst_jmp(condition_meta, condition_label));

    let end_jmp = mir.new_inst_jmp(get_meta(c, s.span), mir.clone_label(end_label));
    push_instr(c, end_jmp);
}

def temp_addr(c: *MirCompiler, ty: *ty.Type): mir.Address {
    let name = bind_s(c, 2, "_t");
    return mir.make_addr_name(name, ty);
}

def bind_ident(c: *MirCompiler, i: *ident.Ident): name.Ref {
    return nt.bind(&c.names, name.make_ref(ident.len(i), ident.start(i)));
}

def bind_s(c: *MirCompiler, len: i32, s: string): name.Ref {
    return nt.bind(&c.names, name.make_ref(len, s));
}

def mod_at(c: *MirCompiler, i: i32): *mod.Module {
    return mod.get_mod(c.data.mods, i);
}

def compile_expr(c: *MirCompiler, e: *ast.Expr, rhs: bool): mir.Expression {
    let a = mir.create_addr_empty();
    if address_expression(c, e, &a) {
        return mir.new_expr_copy(a);
    }

    return complex_expr(c, e, rhs);
}

def assign_expr(c: *MirCompiler, e: *ast.Expr, rhs: bool): mir.Expression {
    let assign = ast.as_assign_expr(e);
    let target = expr_instruction(c, assign.left, rhs);
    let value = compile_expr(c, assign.right, true);

    let meta = get_meta(c, e.span);
    push_assign(c, target, value, meta);
    return mir.new_expr_copy(target);
}

def unary_expr(c: *MirCompiler, e: *ast.Expr, rhs: bool): mir.Expression {
    let unary = ast.as_unary_expr(e);
    let value_addr = expr_instruction(c, unary.right, rhs);
    let meta = get_meta(c, e.span);

    if ast.unary_is_deref(unary) {
        // Expressions on the right side of an assignment have to be derefed twice
        // since a deref is just load instruction
        if rhs {
            let value = mir.new_expr_unary_deref(value_addr);
            push_temp_assign(c, value, e.ty, meta, &value_addr);
        }

        return mir.new_expr_unary_deref(value_addr);
    }

    if ast.unary_is_ref(unary) {
        let ref_name = mir.as_addr_name(&value_addr).value;
        let ref = mir.make_addr_ref(ref_name, e.ty);
        return mir.new_expr_copy(ref);
    }

    if ast.unary_is_neg_bool(unary) {
        return mir.new_expr_unary_bool_neg(value_addr);
    }

    if ast.unary_is_neg_num(unary) {
        return mir.new_expr_unary_num_neg(value_addr);
    }

    return mir.empty_expr();
}

def binary_expr(c: *MirCompiler, e: *ast.Expr): mir.Expression {
    let binary = ast.as_binary_expr(e);

    let left = expr_instruction(c, binary.left, true);
    let right = expr_instruction(c, binary.right, true);

    let kind = mir_binary_kind_from_expr(binary);
    return mir.new_expr_binary(kind, left, right);
}

def call_expr(c: *MirCompiler, e: *ast.Expr, rhs: bool): mir.Expression {
    // The left side may be an arbitrary expression, so we need to be careful
    // here. Example:
    // f()()
    // _t0 = f()
    // _t1 = _t0()

    let call = ast.as_call_expr(e);
    let func_addr = expr_instruction(c, call.callee, rhs);
    let varargs = ty.as_func(call.callee.ty).is_varargs;

    let i = 0;
    let n = ast.num_args(&call.args);
    let args = vec.with_cap(sizeof mir.Address, n);
    while i < n {
        let arg = ast.get_arg_at(&call.args, i);
        let arg_addr = expr_instruction(c, arg, true);
        vec.push(&args, &arg_addr as *void);
        i = i + 1;
    }

    return mir.new_expr_call(func_addr, args, varargs);
}

def access_expr(c: *MirCompiler, e: *ast.Expr, rhs: bool): mir.Expression {
    let access = ast.as_access_expr(e);
    dbg.assert(
        ast.is_ident_expr(access.right),
        "Expected right side of access expression to be an IdentExpr"
    );
    let ident = ast.as_ident_expr(access.right).ident;

    // the only other option currently is a module, which we can't really compile
    if !ty.is_struct(access.left.ty) && !ty.is_ptr_type(access.left.ty) {
        return compile_expr(c, access.right, rhs);
    }

    let left_meta = get_meta(c, access.left.span);
    let left_addr = expr_instruction(c, access.left, rhs);
    let left_ty = left_addr.ty;

    if ty.is_ptr_type(left_ty) {
        dbg.assert(ty.is_ptr(left_ty), "strings are not accessible with '.'");

        push_temp_assign(
            c,
            mir.new_expr_copy(left_addr),
            left_ty,
            left_meta,
            &left_addr
        );

        left_ty = ty.as_ptr(left_ty).inner;
    }

    dbg.assert_fmt(
        ty.is_struct(left_ty),
        "Expected struct, but got %s",
        ty.to_static_string(left_ty)
    );

    let struct_ty = ty.as_struct(left_ty);
    let field_idx = ty.field_idx(struct_ty, ident);
    dbg.assert(field_idx >= 0, "no field with this name");

    return mir.new_expr_gep(left_addr, field_idx);
}

def init_expr(c: *MirCompiler, e: *ast.Expr, rhs: bool): mir.Expression {
    let struct_init = ast.as_init_expr(e);

    let mir_struct = lookup_or_declare_struct(c, e.ty);

    let i = 0;
    let n = ast.num_inits(&struct_init.inits);
    let inits = vec.with_cap(sizeof mir.Address, n);

    while i < n {
        let init = ast.init_expr_at(&struct_init.inits, i);
        let init_addr = expr_instruction(c, init, true);
        vec.push(&inits, &init_addr as *void);
        i = i + 1;
    }

    return mir.new_expr_struct_init(mir_struct.name, inits);
}

def complex_expr(c: *MirCompiler, e: *ast.Expr, rhs: bool): mir.Expression {
    // EXPR_ACCESS
    if ast.is_access_expr(e) {
        return access_expr(c, e, rhs);
    }

    // EXPR_BINARY
    if ast.is_binary_expr(e) {
        return binary_expr(c, e);
    }

    // EXPR_ASSIGN
    if ast.is_assign_expr(e) {
        return assign_expr(c, e, rhs);
    }

    // EXPR_UNARY
    if ast.is_unary_expr(e) {
        return unary_expr(c, e, rhs);
    }

    // EXPR_NEW
    if ast.is_new_expr(e) {
        let new_e = ast.as_new_expr(e);
        let addr = expr_instruction(c, new_e.expr, true);
        return mir.new_expr_new(addr);
    }

    // EXPR_SIZEOF
    if ast.is_sizeof_expr(e) {
        let so = ast.as_sizeof_expr(e);
        // TODO(#81): in theory, this should return a AddressConst with e.ty.width as
        //  its value. But currently i don't really trust the width calculation
        //  algorithm, so we still use ExprSizeOf, even though it should be
        //  unnecessary (when this todo is removed, the entire mir.ExprSizeOf should
        //  be removed as well)
        //  PS: remember that sizeof(void) := 1
        return mir.new_expr_sizeof(so.actual_ty);
    }

    // EXPR_AS
    if ast.is_as_expr(e) {
        let e_as = ast.as_as_expr(e);
        let addr = expr_instruction(c, e_as.expr, rhs);

        return mir.new_expr_bitcast(addr, e.ty);
    }

    // EXPR_CALL
    if ast.is_call_expr(e) {
        return call_expr(c, e, rhs);
    }

    // EXPR_INIT
    if ast.is_init_expr(e) {
        return init_expr(c, e, rhs);
    }

    // dbg.assert_fmt(false, "expr kind %d not handled", e.tag);
    return mir.empty_expr();
}

// creates one or multiple instructions for the given expression and returns
// the address of the result
def expr_instruction(c: *MirCompiler, e: *ast.Expr, rhs: bool): mir.Address {
    let a = mir.create_addr_empty();
    if address_expression(c, e, &a) {
        return a;
    }

    let mir_expr = complex_expr(c, e, rhs);
    if mir.is_expr_copy(&mir_expr) {
        a = mir.as_expr_copy(&mir_expr).addr;
        mir.free_expr(&mir_expr);
        return a;
    }

    let meta = get_meta(c, e.span);

    // the result of a void function should be assigned to an empty address
    if mir.is_expr_call(&mir_expr) {
        let f_ty = ty.as_func(mir.as_expr_call(&mir_expr).callee.ty);
        if ty.is_void(f_ty.ret) {
            // assign result to empty address (a is always empty here)
            push_assign(c, a, mir_expr, meta);
            return a;
        }
    }

    push_temp_assign(c, mir_expr, e.ty, meta, &a);
    push_decl(c, mir.as_addr_name(&a).value, e.ty, meta);
    return a;
}

// checks if the supplied expression is an "address expression", so either
// a name, or an rvalue. If it is, returns true and fills the out parameter
// with the correct address
def address_expression(c: *MirCompiler, e: *ast.Expr, out: *mir.Address): bool {
    if ty.is_func(e.ty) {
        // currently all functions are static, this has to change, when we introduce
        // function pointers

        let mir_func = lookup_or_declare_function(c, e.ty);
        *out = mir.make_addr_name(mir_func.name, e.ty);
        return true;
    }

    if ast.is_ident_expr(e) {
        let ident = &ast.as_ident_expr(e).ident;
        let ref_name = name_from_ident(ident);
        let mangled = name.empty();
        if nt.lookup(&c.names, ref_name, &mangled) {
            *out = mir.make_addr_name(mangled, e.ty);
            return true;
        }

        dbg.assert_fmt(false, "undefined: %.*s", ref_name.len, ref_name.data);
    }

    return rvalue_expr(c, e, out);
}

// Checks if the expression (e) is a rvalue. If it is, the out_address is filled
// with the correct mir.Address for this value
// (e.g. AddressConst for an int literal)
def rvalue_expr(c: *MirCompiler, e: *ast.Expr, out: *mir.Address): bool {
    if ast.is_int_expr(e) {
        *out = mir.make_addr_const(e.ty, e.span);
        return true;
    }

    if ast.is_float_expr(e) {
        *out = mir.make_addr_const(e.ty, e.span);
        return true;
    }

    if ast.is_null_expr(e) {
        *out = mir.create_addr_null(e.ty);
        return true;
    }

    if ast.is_string_expr(e) {
        let name = string_lit(c, e);
        *out = mir.make_addr_global(name, e.ty);
        return true;
    }

    if ast.is_char_expr(e) {
        *out = mir.make_addr_const(e.ty, e.span);
        return true;
    }

    return false;
}

def string_lit(c: *MirCompiler, e: *ast.Expr): name.Ref {
    return add_global(c, e.span, e.ty);
}

def add_global(c: *MirCompiler, span: span.Span, ty: *ty.Type): name.Ref {
    let meta = get_meta(c, span);

    let idx = c.globals.len;
    let s_len = 0;
    let s = std.l_format_str(&s_len, "_g%d", idx);
    let mangled_name = s_mangle(c, s_len, s);

    let key = map.create_key(mangled_name.len, mangled_name.data);
    let const = new mir.create_const(mangled_name, meta, ty, span);
    map.insert(&c.globals, key, const as *void);

    delete s;
    return name.make_ref(mangled_name.len, mangled_name.data);
}

def label(c: *MirCompiler): mir.Label {
    let s_len = 0;
    let s = std.l_format_str(&s_len, "L_%d", c.label_count);
    c.label_count = c.label_count + 1;

    let mangled_name = s_mangle(c, s_len, s);
    delete s;

    return mir.create_label(mangled_name);
}

def s_mangle(c: *MirCompiler, len: i32, s: string): name.Owned {
    return name.s_mangle(
        c.data.proj_root,
        len,
        s,
        mod.get_path(c.mod),
        mod.is_std_lib(c.mod)
    );
}

def name_from_ident(i: *ident.Ident): name.Ref {
    return name.make_ref(ident.len(i), ident.start(i));
}

#include "mir_types.h"
#include "expr_types.h"

def mir_binary_kind_from_expr(binary: *ast.BinaryExpr): i32 {
    let k = binary.kind;
    let left = binary.left;
    let right = binary.right;

    // the binary kinds for ast and mir only differ for the scalar addition
    // and subtraction...
    if k == BINARY_ADD {
        // the following combinations are possible:
        // left | right
        //  num |  num
        //  int |  ptr
        //  ptr |  int
        if ty.is_num(left.ty) && ty.is_num(right.ty) {
            return MIR_BIN_ADD;
        }

        return MIR_BIN_ADD_SCALAR;
    } else if k == BINARY_SUB {
        // the following combinations are possible:
        // left | right
        //  num |  num
        //  int |  ptr
        //  ptr |  int
        //  ptr |  ptr   <- this is not allowed for addtion

        if ty.is_num(left.ty) && ty.is_num(right.ty) {
            return MIR_BIN_SUB;
        }

        if ty.is_ptr_type(left.ty) && ty.is_ptr_type(right.ty) {
            return MIR_BIN_SUB;
        }

        return MIR_BIN_SUB_SCALAR;
    }

    // ...everything else is identical
    return k;
}
