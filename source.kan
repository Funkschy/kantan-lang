import "path";
import "file";
import "span";
import "std";
import "ptrvec";

import "dbg";

def kantan_file_ending(len: *i32): string {
    if len != null {
        *len = 4;
    }
    return ".kan";
}

type LineSpan struct {
    pos: SourcePos,
    span: span.Span
}

type SourcePos struct {
    lnr: i32, // line number. Starting from 1
    col: i32  // column number. Starting from 1
}

def empty_pos(): SourcePos {
    return SourcePos { lnr: 0, col: 0 };
}

type SourceFile struct {
    is_std_lib: bool,
    len: i32,
    code: string,
    file: file.File,
    map: SourceMap
}

def free_sf(sf: *SourceFile): void {
    delete sf.code;
    file.free_f(&sf.file);
    free_srcmap(&sf.map);
}

def file_name_dup(sf: *SourceFile): string {
    return file.name_dup(&sf.file);
}

def code(sf: *SourceFile): string {
    return sf.code;
}

def read(p: path.PathBuf, f: *SourceFile, is_std_lib: bool): i32 {
    let new_file = file.create(p);
    let s: string = null;
    let len = 0;
    let res = file.read_to_string(&new_file, &s, &len);

    if res != 0 {
        return res;
    }

    let map = map_from_source(s, len);
    *f = SourceFile {
        is_std_lib: is_std_lib,
        len: len,
        code: s,
        file: new_file,
        map: map
    };

    return 0;
}

def line_info(
    file: *SourceFile,
    target: span.Span,
    out_line_start: *string,
    out_line_end: *string,
    out_line_nr: *i32,
    out_col: *i32
): void {
    dbg.assert_fmt(
        file.code <= target.start && target.end <= file.code + file.len,
        "invalid target (%p-%p) for source (%p-%p) in %.*s",
        target.start, target.end,
        file.code, file.code + file.len,
        file.file.path.inner.len, file.file.path.inner.inner
    );

    let last_nl = target.start;

    let ptr = target.start;
    while ptr > file.code {
        if *ptr == '\n' {
            last_nl = ptr;
            ptr = file.code; // there is currently no 'break' keyword
        } else {
            ptr = ptr - 1;
        }
    }

    // there was no new line => span is in first line of file
    if last_nl == target.start {
        last_nl = file.code - 1; // we add a 1 later, which is correct in every other case
    }

    // the \n at the end of the line with span in it
    if out_line_end != null {
        *out_line_end = std.find_char(target.end, '\n');
    }
    if out_line_start != null {
        *out_line_start = last_nl + 1;
    }
    if out_col != null {
        *out_col = target.start - last_nl;
    }
    if out_line_nr != null {
        *out_line_nr = find_line_in_srcmap(&file.map, target);
    }
}

def find_line(file: *SourceFile, target: span.Span): LineSpan {
    let pos = SourcePos{ lnr: 0, col: 0 };
    let line_span = span.empty();

    line_info(file, target, &line_span.start, &line_span.end, &pos.lnr, &pos.col);

    return LineSpan { pos: pos, span: line_span };
}

def span_to_pos(file: *SourceFile, s: span.Span): SourcePos {
    let pos = SourcePos{ lnr: 0, col: 0 };
    line_info(file, s, null, null, &pos.lnr, &pos.col);
    return pos;
}

// This is used to convert Spans into SourcePos' quickly
type SourceMap struct {
    line_starts: ptrvec.Vec // ptrvec.Vec<string>
}

def map_from_source(code: string, len: i32): SourceMap {
    let line_estimate = len / 24; // assume that a line has 24 chars on avg
    let vec = ptrvec.with_cap(line_estimate);
    let pos = code;
    let end = code + len;

    while pos != null && pos != end {
        vec.push_ptr(pos as *void);
        pos = std.find_char(pos, '\n');
        if pos != null {
            pos = pos + 1;
        }
    }

    return SourceMap { line_starts: vec };
}

// does a binary search inside the SourceMap, to find the line number of the
// specified span
def find_line_in_srcmap(m: *SourceMap, span: span.Span): i32 {
    let start = 0;
    let end = m.line_starts.len - 1;

    while start <= end {
        let idx = (start + end) / 2;
        let line_start = line_at(m, idx);
        let line_end = line_at(m, idx + 1);

        if line_end == null {
            // no next line, so just search for the end of the string
            line_end = std.strchr(line_start, 0);
        }
        line_end = line_end - 1;

        if line_start <= span.start && span.start <= line_end {
            // span is in this line
            return idx + 1;
        } else if span.start > line_start {
            // span is to the right of current line
            start = idx + 1;
        } else {
            // span is to the left of current line
            end = idx - 1;
        }
    }

    return -1;
}

def line_at(m: *SourceMap, idx: i32): string {
    return m.line_starts.get(idx) as string;
}

def free_srcmap(m: *SourceMap): void {
    m.line_starts.free();
}
