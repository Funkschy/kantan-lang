import "ty";
import "dbg";
import "vec";
import "std";
import "span";
import "tyid";
import "ident";
import "lexer";
import "typaram";

type Param struct {
    name: ident.Ident,
    ty: tyid.Type
}

def create_param(name: ident.Ident, ty: tyid.Type): Param {
    return Param { name: name, ty: ty };
}

def is_varargs_param(p: *Param): bool {
    return std.memcmp(ident.start(&p.name) as *void, "..." as *void, 3) == 0;
}

def (p: *Param) clone(): Param {
    return Param { name: p.name, ty: p.ty.clone() };
}

def (p: *Param) free() {
    p.ty.free();
}

type ParamList struct {
    params: vec.Vec
}

def pl_from_vec(params: vec.Vec): ParamList {
    return ParamList { params: params };
}

def free_pl(pl: *ParamList) {
    for let i: usize = 0; i < pl.params.len; i += 1 {
        (pl.params.get_ptr(i) as *Param).free();
    }
    pl.params.free();
}

def (pl: *ParamList) clone(): ParamList {
    let new_params = vec.with_cap(sizeof Param, pl.params.len);
    for let i: usize = 0; i < pl.params.len; i += 1 {
        let p = (pl.params.get_ptr(i) as *Param).clone();
        new_params.push(&p as *void);
    }
    return ParamList { params: new_params };
}

def add_param(pl: *ParamList, p: *Param) {
    pl.params.push(p as *void);
}

type Kind enum {
    Method, Procedure, Extern, Delegate
}

type FuncDef struct {
    kind: Kind,
    exported: bool,
    name: ident.Ident,
    receiver: Param,
    ret_ty: tyid.Type,
    params: ParamList,
    type_params: typaram.List,
    signature: *ty.Type
}

def create_func(
    name: lexer.Token,
    params: ParamList,
    type_params: typaram.List,
    ret_ty: tyid.Type,
    kind: Kind
): FuncDef {
    dbg.assert(kind != Kind.Method, "use create_method instead");

    return FuncDef {
        kind: kind,
        exported: false,
        name: ident.create(name),
        receiver: undefined,
        ret_ty: ret_ty,
        params: params,
        type_params: type_params,
        signature: null
    };
}

def create_method(
    name: lexer.Token,
    receiver: Param,
    params: ParamList,
    type_params: typaram.List,
    ret_ty: tyid.Type
): FuncDef {
    return FuncDef {
        kind: Kind.Method,
        exported: false,
        name: ident.create(name),
        receiver: receiver,
        ret_ty: ret_ty,
        params: params,
        type_params: type_params,
        signature: null
    };
}

def (f: *FuncDef) is_main(): bool {
    return ident.eq_lit(&f.name, "main");
}

def (f: *FuncDef) get_param_ptr(i: u32): *Param {
    return f.params.params.get_ptr(i as usize) as *Param;
}

def (f: *FuncDef) num_params(): u32 {
    return f.params.params.len as u32;
}

def (f: *FuncDef) get_type_param_ptr(i: u32): *typaram.TypeParam {
    return f.type_params.get_ptr(i as usize);
}

def (f: *FuncDef) num_type_params(): u32 {
    return f.type_params.len() as u32;
}

def (f: *FuncDef) is_extern(): bool {
    return f.kind == Kind.Extern;
}

def (f: *FuncDef) is_exported(): bool {
    return f.exported;
}

def (f: *FuncDef) is_delegate(): bool {
    return f.kind == Kind.Delegate;
}

def (f: *FuncDef) is_method(): bool {
    return f.kind == Kind.Method;
}

def (f: *FuncDef) is_procedure(): bool {
    return f.kind == Kind.Procedure;
}

def (f: *FuncDef) clone(): FuncDef {
    let new_def = *f;
    new_def.params = f.params.clone();
    if f.kind == Kind.Method {
        new_def.receiver = f.receiver.clone();
    }
    new_def.ret_ty = f.ret_ty.clone();
    new_def.type_params = f.type_params.clone();
    return new_def;
}

def (f: *FuncDef) free() {
    free_pl(&f.params);
    if f.kind == Kind.Method {
        f.receiver.free();
    }
    f.ret_ty.free();
    f.type_params.free();
}
