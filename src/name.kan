import ":std/str";
import "mod";
import ":std/dbg";
import "std";
import "path";
import "ident";
import "source";

type Owned struct {
    data: string,
    len: usize
}

def free_owned(n: *Owned): void {
    delete n.data;
}

def make_owned(len: usize, data: string): Owned {
    if data != null {
        dbg.assert(*(data + len) == '\0', "owned names should be null terminated");
    }
    return Owned { data: data, len: len };
}

// like make_owned, but copies the data automatically
def make_copied(len: usize, data: string): Owned {
    return make_owned(len, std.strndup(data, len));
}

def (n: *Owned) pushed(s: str.View): Owned {
    defer free_owned(n);

    let len = n.len + s.len;
    let buffer = std.malloc(len + 1);
    std.memcpy(buffer, n.data as *void, n.len);
    std.memcpy(buffer + n.len, s.data as *void, s.len);
    let buffer = buffer as *i8;
    *(buffer + len) = '\0';
    return make_owned(len, buffer);
}

def (n: *Owned) clone(): Owned {
    return make_copied(n.len, n.data);
}

def (n: *Owned) ref(): Ref {
    return ref(*n);
}

def (n: *Owned) as_view(): str.View {
    return str.view(n.data, n.len);
}


def (n: *Owned) to_string(): str.String {
    return str.from_l(n.data, n.len);
}

type Ref struct {
    len: usize,
    data: string
}

def make_ref(len: usize, data: string): Ref {
    return Ref { len: len, data: data };
}

def ref(owned: Owned): Ref {
    return make_ref(owned.len, owned.data);
}

def (n: *Ref) as_view(): str.View {
    return str.view(n.data, n.len);
}

def (n: *Ref) clone(): Owned {
    return make_copied(n.len, n.data);
}

def (n: *Ref) or_else(static: Ref): Ref {
    if n.data == null {
        return static;
    }
    return *n;
}

def empty(): Ref {
    return make_ref(0, null);
}

def mangle_ident(
    proj_root: path.Path,
    name: *ident.Ident,
    declared_in: *mod.Module
): Owned {
    return s_mangle(
        proj_root,
        ident.len(name),
        ident.start(name),
        declared_in
    );
}

def s_mangle(
    proj_root: path.Path,
    len: usize,
    s: string,
    declared_in: *mod.Module
): Owned {
    let name_len: usize = 0;
    let name_data = "";

    // TODO(#72): this name mangling algorithm assumes the existence of a global
    //  file path root. This does not necessarily exist in windows

    let file_ending_len: usize = 0;
    source.kantan_file_ending(&file_ending_len);

    let p = mod.get_path(declared_in);

    if !mod.is_std_lib(declared_in) {
        let mod_len = p.len - proj_root.len - file_ending_len;
        let mod_name = p.inner + proj_root.len;

        // this is currently the only invalid identifier, so we just handle
        // it as an edge case
        let is_invalid_identifier = std.strncmp(mod_name, "llvm", 4) == 0;
        if is_invalid_identifier {
            mod_len = mod_len + 1;
        }

        // +1 for '.'
        name_len = mod_len + len + 1;
        name_data = std.malloc(name_len + 1) as string;

        let out_idx = 0;
        if is_invalid_identifier {
            // escape the invalid name with '_'
            *name_data = '_';
            out_idx = 1;
        }

        let in_idx: usize = 0;
        let sep = path.sys_separator();
        while in_idx < mod_len {
            let c = *(mod_name + in_idx);
            if c == sep {
                c = '.';
            }
            *(name_data + out_idx) = c;
            in_idx = in_idx + 1;
            out_idx = out_idx + 1;
        }
        *(name_data + mod_len) = '.';

        let dest = name_data + mod_len + 1;
        std.memcpy(dest as *void, s as *void, len);
        *(name_data + name_len) = '\0';
    } else {
        let file_name_len = p.len - file_ending_len;
        let file_name = p.inner;

        name_len = 4 + file_name_len + 1 + len;
        name_data = std.format_str(
            "std.%.*s.%.*s", file_name_len, file_name, len, s
        );
    }

    return make_owned(name_len, name_data);
}

