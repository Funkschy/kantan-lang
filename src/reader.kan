import "vec";
import "std";
import "dbg";
import "path";
import "span";
import "file";
import "source";
import "error" as err;

extern def num_builtin_files(): usize;
extern def get_builtin_files(): *string;

type FileAddResult struct {
    file_name: string,
    err_code: err.CompileError
}

type FileMap struct {
    files: vec.Vec, // vec.Vec<source.SourceFile>
    // the largest shared path of all source files
    // Example:
    // The following files have a common root of '/home/'
    // /home/user/test.kan
    // /home/user/kantan/other.kan
    // /home/program.kan
    //
    // if we removed program.kan, the common root would be '/home/user/'
    proj_root: path.PathBuf
}

def create(): FileMap {
    return FileMap {
        files: vec.create(sizeof source.SourceFile),
        proj_root: path.buf_from(path.empty())
    };
}

def (m: *FileMap) num_files(): usize {
    return m.files.len;
}

def (m: *FileMap) get_ptr_by_idx(idx: usize): *source.SourceFile {
    return m.files.get_ptr(idx) as *source.SourceFile;
}

def (m: *FileMap) get_ptr_from_span(span: span.Span): *source.SourceFile {
    // TODO(#60): this should use a binary search or some kind of tree

    for let i: usize = 0; i < m.files.len; i += 1 {
        let file = m.get_ptr_by_idx(i);
        dbg.assert(file != null, "file was null");

        if span.start >= file.code && span.end <= file.code + file.len {
            return file;
        }
    }

    return null;
}

def (m: *FileMap) free() {
    for let i: usize = 0; i < m.files.len; i += 1 {
        let s: source.SourceFile = undefined;
        if m.files.get(i, &s as *void) {
            s.free();
        }
    }

    m.files.free();
    path.free_pb(&m.proj_root);
}

def (m: *FileMap) add_std_lib(rel_path: *path.Path, std_dir: string): FileAddResult {
    let len = num_builtin_files();
    let file_names = get_builtin_files();

    for let i: usize = 0; i < len; i += 1 {
        let file_name = *(file_names + i);
        let full_file_name = std.format_str("%s/%s", std_dir, file_name);
        defer delete full_file_name;

        let res = m.add_file(rel_path, full_file_name, true);
        if res.is_not_ok() {
            return FileAddResult { file_name: file_name, err_code: res };
        }

        let idx = m.num_files() - 1;
        let sptr = m.get_ptr_by_idx(idx);

        if sptr != null {
            let new_path = path.new_path_buf(file_name);
            sptr.file.free();
            sptr.file.path = new_path;
        }
    }

    return FileAddResult { file_name: null, err_code: err.CompileError.OK };
}

def (m: *FileMap) add_all(rel_path: *path.Path, len: usize, file_names: *string): FileAddResult {
    // TODO(#31): detect duplicate files
    for let i: usize = 0; i < len; i += 1 {
        let file_name = *(file_names + i);
        let res = m.add_file(rel_path, file_name, false);
        if res.is_not_ok() {
            return FileAddResult { file_name: file_name, err_code: res };
        }
    }

    return FileAddResult { file_name: null, err_code: err.CompileError.OK };
}

def (m: *FileMap) add_file(rel_path: *path.Path, name: string, is_std_lib: bool): err.CompileError {
    let p = path.empty();
    let res = path.from_str(name, &p);
    if res.is_not_ok() {
        return res;
    }

    let abs_path = path.buf_from(path.empty());
    let ret_val = path.merge_abs_rel(rel_path, &p, &abs_path);
    if ret_val.is_not_ok() {
        return ret_val;
    }

    let sf: source.SourceFile = undefined;
    if (res = source.read(abs_path, &sf, is_std_lib)).is_not_ok() {
        path.free_pb(&abs_path);
        return res;
    }

    m.files.push(&sf as *void);
    if !is_std_lib {
        m.update_root(abs_path.inner);
    }

    return err.CompileError.OK;
}

def (m: *FileMap) update_root(p: path.Path) {
    if m.proj_root.inner.len == 0 {
        let p = path.parent(&p);
        m.proj_root = path.new_path_buf_l(p.len, p.inner);
        return;
    }

    let common = path.common_parent(m.proj_root.inner, p);
    path.free_pb(&m.proj_root);
    m.proj_root = common;
}
