import ":std/io";
import ":std/dbg";
import ":std/vec";
import ":std/map";
import ":std/ptrvec";

import "ty";
import "std";
import "mod";
import "mir";
import "mirc";
import "path";
import "scope";
import "target";
import "config";
import "codegen";
import "llvm-bindings" as llvm;

extern def link_obj_file(obj_name: string, exe_name: string): i32;

// TODO(#98): the unlink function only exists on posix systems, not windows
extern def unlink(filename: string): i32;

// Data which is shared between all Compiler instances
// readonly, since it will be used across threads
type CompilationData struct {
    mods: *mod.Map,
    main_func: *ty.Type,
    proj_root: path.Path,
    global_types: scope.Scope,
    config: *config.Config,
    std_dir: string
}

def map_data(data: *CompilationData): mirc.CompilationData {
    return mirc.CompilationData {
        config: data.config,
        mods: data.mods,
        proj_root: data.proj_root,
        global_types: data.global_types
    };
}

def compile_all(comp_data: CompilationData, types: ptrvec.Vec): void {
    io.printf("Compiling sequentially\n");
    compile_all_sequential(&comp_data, types);
}

def compile_all_parallel(comp_data: *CompilationData, types: ptrvec.Vec) {
    dbg.not_implemented();
    // llvm doesn't handle multithreading very gracefully (we basically
    // need to have everything in one context, but contexts aren't thread safe)
    // But we want to create a single final module, so that we can do lto,
    // therefore, we simply export each modules bitcode into a buffer and then
    // parse those buffers into a single module at the end
    // (see https://lists.llvm.org/pipermail/llvm-dev/2015-June/086296.html)
    //
    // implement something like this:
    //
    // let bc_buffers = ptrvec.with_cap(n);
    // llvm.verify_module(llvm_context.llmod, 0, null);
    // optimize_single(llvm_context.llmod, comp_data.config.opt_level);

    // let buf = llvm.write_bitcode_to_memory_buffer(llvm_context.llmod);
    // if buf == null {
    //     io.printf("Could not write bitcode to buffer");
    // }
    // ptrvec.set(&bc_buffers, i, buf as *void);

    // llvm.dispose_module(llvm_context.llmod);
    // llvm.context_dispose(llvm_context.ctx);

    // let buf = ptrvec.get(&bc_buffers, i) as *llvm.MemoryBuffer;

    // let mod: *llvm.Module = null;
    // if llvm.parse_bitcode_in_context2(base_ctx, buf, &mod) {
    //     io.printf("Could not parse module\n");
    //     std.abort();
    // }

    // llvm.link_modules2(base_mod, mod);

    // llvm.dispose_memory_buffer(buf);
}

def compile_all_sequential(comp_data: *CompilationData, types: ptrvec.Vec) {
    let use_llvm = true;
    let llvm_dump = false;
    let mir_dump = false;
    let enable_threads = false;


    let data = map_data(comp_data);
    let n = mod.num_mods(data.mods);
    dbg.assert(n > 0, "no modules to compile");

    let main_mod: *mod.Module = null;
    if comp_data.main_func != null {
        main_mod = ty.as_signature(comp_data.main_func).declared_in;
    } else {
        main_mod = mod.get_mod(comp_data.mods, 0);
    }

    let target_conf = comp_data.config.target;
    let t = target.create_and_register_target(
        target_conf.arch,
        target_conf.vendor,
        target_conf.sys,
        target_conf.abi
    );
    defer t.free();

    if t.is_error {
        io.printf("%s\n", t.triple_or_error.cstring());
        return;
    }

    let tm = target.create_target_machine(&t);
    defer tm.free();
    let data_layout = llvm.create_target_data_layout(tm.lltm);
    defer llvm.dispose_target_data(data_layout);

    // the single context used for all modules
    let base_ctx = llvm.context_create();
    let base_mod = llvm.module_create_with_name_in_context(main_mod.name.data, base_ctx);

    let mod_path = mod.get_path(main_mod);
    let dir = path.parent(&mod_path);
    let file = path.create(mod_path.inner + dir.len, mod_path.len - dir.len);
    llvm.set_source_file_name(base_mod, file.inner, file.len);

    llvm.set_module_data_layout(base_mod, data_layout);

    let options = codegen.CompilationOptions {
        global_ctx: base_ctx,
        std_dir: comp_data.std_dir,
        target: t,
        debug_info: comp_data.config.emit_debug_info
    };

    for let i: u32 = 0; i < n; i += 1 {
        let m = mod.get_mod(data.mods, i);

        let mir_compiler = mirc.create_mirc(m, &data);
        let mir_mod = mirc.compile(&mir_compiler);

        if mir_dump {
            io.printf("--- mir %s ---\n", mir_mod.actual.name.data);
            mir.dump_module(&mir_mod);
            io.printf("\n");
        }

        let llvm_context = codegen.compile_mir_mod(&mir_mod, &options);
        if llvm_dump {
            llvm.dump_module(llvm_context.llmod);
        }
        llvm.set_module_data_layout(llvm_context.llmod, data_layout);
        llvm.verify_module(llvm_context.llmod, 0, null);
        optimize_single(llvm_context.llmod, comp_data.config.opt_level);

        llvm.link_modules2(base_mod, llvm_context.llmod);

        mirc.free_compiler(&mir_compiler);
        mir.free_mod(&mir_mod);
    }

    if llvm_dump {
        io.printf("--- final ---\n");
        llvm.dump_module(base_mod);
        llvm.verify_module(base_mod, 0, null);
    }

    emit_to_file(comp_data, base_mod, tm);

    llvm.dispose_module(base_mod);
    llvm.context_dispose(base_ctx);
}

def emit_to_file(comp_data: *CompilationData, base_mod: *llvm.Module, tm: target.TargetMachine) {
    let gen_asm = comp_data.config.output_kind == config.OutputKind.ASM;
    let output_file = comp_data.config.output_file;
    let temp_output_file = comp_data.config.temp_output_file;

    let error = target.emit_to_file(&tm, base_mod, temp_output_file, gen_asm);
    if error != null {
        io.printf("%s\n", error);
        llvm.dispose_message(error);
        return;
    }

    if comp_data.config.invoke_linker {
        link_obj_file(temp_output_file.inner, output_file.inner);
        // use unlink instead of remove, to not accidentally delete some directory
        unlink(temp_output_file.inner);
    }
}

def optimize_single(module: *llvm.Module, opt_level: i8) {
    let opt_level = opt_level as u32;

    // replace this with an enum
    let opt_level_none: u32 = 0;
    // let opt_level_less = 1;
    // let opt_level_default = 2;
    // let opt_level_aggressive = 3;

    if opt_level == opt_level_none {
        return;
    }

    // Per clang and rustc, we want to use both kinds.
    let fpm = llvm.create_function_pass_manager_for_module(module);
    defer llvm.dispose_pass_manager(fpm);
    let mpm = llvm.create_pass_manager();
    defer llvm.dispose_pass_manager(mpm);

    let pm_builder = llvm.pass_manager_builder_create();
    llvm.pass_manager_builder_set_opt_level(pm_builder, opt_level);
    defer llvm.pass_manager_builder_dispose(pm_builder);

    llvm.pass_manager_builder_populate_module_pass_manager(pm_builder, mpm);
    llvm.pass_manager_builder_populate_function_pass_manager(pm_builder, fpm);

    llvm.add_promote_memory_to_register_pass(fpm);
    llvm.add_dead_store_elimination_pass(fpm);

    llvm.initialize_function_pass_manager(fpm);
    // Iterate over functions, running the FPM over each
    let func = llvm.get_first_function(module);
    while func != null {
        llvm.run_function_pass_manager(fpm, func);
        func = llvm.get_next_function(func);
    }
    llvm.finalize_function_pass_manager(fpm);

    // Run the MPM over the module
    llvm.run_pass_manager(mpm, module);
}
